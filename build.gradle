// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    ext {
        // App version
        majorVersion = 0
        minorVersion = 5
        patchVersion = 3
        versionClassifier = 'alpha-2'

        minSdkVersion = 19

        // Build dependencies
        androidPluginVersion = '3.4.0'

        // App dependencies
        androidxAppCompatVersion = '1.1.0-alpha04'
        androidxCardViewVersion = '1.0.0'
        androidxConstraintLayoutVersion = '2.0.0-alpha5'
        androidxRecyclerViewVersion = '1.1.0-alpha04'
        androidxLifecycleVersion = '2.1.0-alpha04'

        googleAndroidMaterialVersion = '1.0.0'

        okHttpVersion = '3.14.1'
        gsonVersion = '2.8.5'
        retrofitVersion = '2.5.0'
        timberVersion = '4.7.1'
        threeTenVersion = '1.2.0'

        // Test dependencies
        testRunnerVersion = '1.2.0-alpha04'
        espressoVersion = '3.2.0-alpha04'
        junitVersion = '4.13-beta-2'
    }

    repositories {
        google()
        jcenter()
        
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$androidPluginVersion"
        
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

private Integer generateVersionCode() {
    return ext.minSdkVersion * 10000000 + ext.majorVersion * 10000 + ext.minorVersion * 100 + ext.patchVersion
}

private String generateVersionName() {
    String versionName = "${ext.majorVersion}.${ext.minorVersion}.${ext.patchVersion}"

    if (ext.versionClassifier != null) {
        versionName += "-" + ext.versionClassifier
    }

    return versionName
}

allprojects {
    repositories {
        google()
        jcenter()
        
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
